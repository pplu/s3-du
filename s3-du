#!/usr/bin/env perl

use strict;
use warnings;
use v5.10;

use Paws;
use Data::Dumper;

my $bucket = $ARGV[0];

my $s3 = Paws->service('S3', region => 'eu-west-1');

if (not defined $bucket) {
  my $list = $s3->ListBuckets;
  print Dumper($list);
} else {
  my $tree = {};
  my $token;
  do {
    my $res = $s3->ListObjectsV2(
      Bucket => $bucket,
      (defined $token) ? (ContinuationToken => $token) : (),
    );

    process_elements($tree, $res->Contents);

    if ($res->IsTruncated) {
      $token = $res->NextContinuationToken;
    } else {
      $token = undef;
    }
  } until (not defined $token);

  print Dumper($tree);
}

sub process_elements {
  my ($tree, $objects) = @_;

  for my $file (@$objects) {
    my $size = $file->Size;
    my @path = split /\//, $file->Key;
    pop @path; # take the file name out...
    while (@path) {
      my $partial_path = join '/', @path;

      $tree->{ $partial_path }->{ _size }  += $size;
      $tree->{ $partial_path }->{ _count } += 1;

      pop @path;
    }
  }
}

